plugins {
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'
apply plugin: 'maven-publish'
apply plugin: 'pl.allegro.tech.build.axion-release'

android {
    compileSdkVersion 31
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 31
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
        singleVariant('debug') {
            withSourcesJar()
        }
    }
    ndkVersion "25.2.9519653"
}

cargo {
    module  = "../arklib"
    libname = "arklib"
    targets = ["arm64", "x86", "x86_64", "arm"]
    // speed up local build
    // targets = ["arm64"]
    prebuiltToolchains = true
    // Set to "debug" to enable logging. Or "release" to make a production build.
    // https://github.com/mozilla/rust-android-gradle/issues/38
    profile = gradle.startParameter.taskNames.any{it.toLowerCase().contains("debug")} ? "debug" : "release"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    implementation 'org.apache.tika:tika-core:2.4.0'

    implementation 'com.github.ARK-Builders:ark-filepicker:ae2c37ea87'

    implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever:1.0.14'

    implementation "com.google.dagger:dagger:2.41"

    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.github.MikeOrtiz:TouchImageView:3.1.1'

    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders')) {
        task.dependsOn 'cargoBuild'
    }
}

// def libVersion = scmVersion.version
def libVersion = '999'

publishing {
    publications {
        release(MavenPublication) {
            groupId = "space.taran"
            artifactId = "arklib"
            version = libVersion
            afterEvaluate {
                from components.release
            }
        }
        debug(MavenPublication) {
            groupId = "space.taran"
            artifactId = "arklib-debug"
            version = libVersion
            afterEvaluate {
                from components.debug
            }
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/ARK-Builders/arklib-android"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }

    }
}

repositories {
    mavenCentral()
}
